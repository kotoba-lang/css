(ns css.build-test
  (:require [clojure.test :refer [deftest is testing]]
            [css.build :as build]))

(deftest deterministic-build
  (let [config {:sheet {:rules {"body" {:color :white :margin 0}}}
                :utility-classes ["flex" "gap-2" "text-[11px]"]}
        a (build/compile-css config)
        b (build/compile-css config)]
    (is (= (:css a) (:css b)))
    (is (re-find #"\.gap-2" (:css a)))
    (is (re-find #"\.text-\\\[11px\\\]" (:css a)))))

(deftest validation-and-strict-utilities
  (testing "invalid sheets fail"
    (is (thrown? Exception (build/compile-css {:sheet {:rules {"" []}}}))))
  (testing "unknown utilities can be warnings or strict failures"
    (is (= :unsupported-utility
           (-> (build/compile-css {:utility-classes ["unknown-x"]}) :warnings first :type)))
    (is (thrown? Exception
                 (build/compile-css {:utility-classes ["unknown-x"] :strict-utilities? true})))))

(deftest responsive-and-arbitrary-layout-utilities
  (let [output (:css (build/compile-css
                      {:utility-classes ["h-8" "max-w-[85%]" "md:flex-row"
                                         "md:grid-cols-[18rem_minmax(0,1fr)]"]
                       :strict-utilities? true}))]
    (is (re-find #"\.h-8" output))
    (is (re-find #"max-width: 85%" output))
    (is (re-find #"@media \(min-width: 48rem\)" output))
    (is (re-find #"\.md\\:flex-row" output))
    (is (re-find #"grid-template-columns: 18rem minmax\(0,1fr\)" output))))

(deftest indexes-shadow-compatible-static-css
  (let [indexed (build/index-shadow-source "test/css/shadow_fixture.clj")
        entry (first indexed)
        output (build/compile-css {:shadow-sources ["test/css/shadow_fixture.clj"]})]
    (is (= 1 (count indexed)))
    (is (re-find #"css_shadow-fixture__L\d+C\d+" (:class entry)))
    (is (re-find #"display: flex" (:css output)))
    (is (re-find #"color: red" (:css output)))
    (is (re-find #":hover" (:css output)))
    (is (re-find #"@media \(min-width: 40rem\)" (:css output)))))
